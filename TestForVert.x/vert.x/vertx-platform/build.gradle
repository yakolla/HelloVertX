/*
 * Copyright (c) 2011-2013 The original author or authors
 * ------------------------------------------------------
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * and Apache License v2.0 which accompanies this distribution.
 *
 *     The Eclipse Public License is available at
 *     http://www.eclipse.org/legal/epl-v10.html
 *
 *     The Apache License v2.0 is available at
 *     http://www.opensource.org/licenses/apache2.0.php
 *
 * You may elect to redistribute this code under either of these licenses.
 */

import java.text.SimpleDateFormat

apply from: "$rootDir/gradle/maven.gradle"

dependencies {
	compile project(':vertx-core')
}

artifacts {
	platform jar
}

jar {
	manifest {
		attributes(
        'Main-Class': 'org.vertx.java.deploy.impl.cli.Starter',
        'Implementation-Title': 'Vertx (see http://vertx.io/)',
        'Vertx-Version': version + " (built " + new SimpleDateFormat("yyyy-MM-dd HH:mm:ss").format(new Date()) + ")"
		)
	}

  exclude 'logging.properties'
}


// Cleanup the modules placed in the mod-test directory during the test suite
task cleanTestTmpModules(type: Delete) {
  delete { file("src/test/mod-test").listFiles().findAll {
    it.name ==~ /io.vertx~lang-rhino.+/ ||
    it.name ==~ /io.vertx~mod-maven-test.+/ ||
    it.name ==~ /purplefox~mod-bintray-test.+/
    }
  }
  delete { file(".").listFiles().findAll {
    it.name ==~ /io.vertx#my-mod.+/
    }
  }
  delete 'reloader-test-mods'
}

clean {
  dependsOn cleanTestTmpModules
}

test {
  dependsOn cleanTestTmpModules
}

test {
  outputs.upToDateWhen { false }

  testLogging.showStandardStreams = true

  systemProperty 'vertx.test.timeout', 60
  systemProperty 'vertx.mods', "$projectDir/src/test/mod-test"

  jvmArgs '-server', '-Xms128m', '-Xmx1024m', '-XX:MaxPermSize=128m', '-XX:NewRatio=2'

  testLogging { exceptionFormat "full" }
}
